allprojects {
    apply plugin: 'java'
    apply plugin: 'signing'
    apply plugin: 'maven-publish'

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    group = "net.sunshow.toolkit"
    version = "2.0-SNAPSHOT"

    compileJava.options.encoding = "UTF-8"
    compileTestJava.options.encoding = "UTF-8"

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    tasks.withType(Test) {
        systemProperty "file.encoding", "UTF-8"
    }

    repositories {
//        maven {
//            url 'https://maven.aliyun.com/nexus/content/groups/public/'
//        }
        mavenCentral()
    }

    dependencies {
        implementation(platform(rootProject.libs.bom.springboot))
    }

    publishing {

        // 发布 release
        version = "2.2.2"

        // 打包源码
        task sourcesJar(type: Jar) {
            from sourceSets.main.allJava
            archiveClassifier = 'sources'
        }

        // JavaDoc
        task javadocJar(type: Jar) {
            from javadoc
            archiveClassifier = 'javadoc'
        }

        publications {
            mavenJava(MavenPublication) {
                from components.java

                // 源码
                artifact sourcesJar

                // JavaDoc
                artifact javadocJar

                // pom
                pom {
                    name = 'Sunshow Toolkit Core'
                    description = 'A toolkit for Java'
                    url = 'https://github.com/Sunshow/sunshow-toolkit'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'sunshow'
                            name = 'Sunshow'
                            email = 'sunshow@gmail.com'
                        }
                    }
                    scm {
                        connection = 'https://github.com/Sunshow/sunshow-toolkit'
                        developerConnection = 'https://github.com/Sunshow/sunshow-toolkit'
                        url = 'https://github.com/Sunshow/sunshow-toolkit'
                    }
                }
            }
        }

        if (project.hasProperty("publishUsername") && project.hasProperty("publishPassword")
                && project.hasProperty("publishReleasesRepoUrl") && project.hasProperty("publishSnapshotsRepoUrl")
        ) {
            repositories {
                maven {
                    url = version.toString().endsWith("SNAPSHOT") ? project.findProperty("publishSnapshotsRepoUrl") : project.findProperty("publishReleasesRepoUrl")
                    allowInsecureProtocol true

                    credentials {
                        username project.findProperty("publishUsername")
                        password project.findProperty("publishPassword")
                    }
                }
            }
        }
    }

    signing {
        sign publishing.publications.mavenJava
    }

}